group 'com.bitjourney.plantuml'
version '1.0.8'

final MainClassName = "${group}.Main"

// See http://plantuml.com/changes
final PlantUmlVersion = "2017.11"

final ArchiveName = "plantuml-service.jar"

buildscript {
    ext.kotlin_version = '1.1.1'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'

dependencies {
    compile "net.sourceforge.plantuml:plantuml:$PlantUmlVersion"
    compile "com.sparkjava:spark-core:2.5.5"
    compile "com.github.ben-manes.caffeine:caffeine:2.4.0"
    compile "org.slf4j:slf4j-simple:1.7.21"
    compile "com.google.code.gson:gson:2.8.0"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.6.2'
}
repositories {
    jcenter()
}

jar {
    archiveName ArchiveName
    manifest {
        attributes(
                "Main-Class": MainClassName,
                "Specification-Version": PlantUmlVersion,
                "Implementation-Title": "plantuml-service",
                "Implementation-Version": version,
                "Implementation-Vendor": "BitJourney, Inc."
        )
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task stage(type: Copy, dependsOn: jar) {
    from file("build/libs/${ArchiveName}")
    into file("bin/")
}

if (new File(System.getenv("HOME"), ".github").exists()) {
    ext.githubOwner = "bitjourney"
    ext.githubRepo = "plantuml-service"
    ext.artifactFile = file("bin/plantuml-service.jar")
    ext.artifactContentType = "application/java-archive"
    ext.version = version

    apply from: './release.gradle'
}
